# Setup Homebrew and zsh completions
if [[ "$OSTYPE" == *android* ]]; then
  # Do not probe root-level folders on Android
  BREW_LOCATION=
# Disregard any `brew` already in the path and probe for
# specific folders.
elif [[ -x /opt/homebrew/bin/brew ]]; then
  # macOS on Apple Silicon
  BREW_LOCATION="/opt/homebrew/bin/brew"
elif [[ -x /usr/local/bin/brew ]]; then
  # Legacy macOS on Intel
  BREW_LOCATION="/usr/local/bin/brew"
elif [[ -x /home/linuxbrew/.linuxbrew/bin/brew ]]; then
  # Homebrew Linux global (formerly Linuxbrew)
  BREW_LOCATION="/home/linuxbrew/.linuxbrew/bin/brew"
elif [[ -x "$HOME/.linuxbrew/bin/brew" ]]; then
  # Homebrew Linux local
  BREW_LOCATION="$HOME/.linuxbrew/bin/brew"
else
  # No Homebrew
  BREW_LOCATION=
fi

if [ -n "$BREW_LOCATION" ]; then
  eval "$("$BREW_LOCATION" shellenv)"

  # Set HOMEBREW_PREFIX if necessary
  if [[ -z "$HOMEBREW_PREFIX" ]]; then
    export HOMEBREW_PREFIX="$(brew --prefix)"
  fi

  # Add zsh completions to $fpath
  for DIR in "$HOMEBREW_PREFIX/share/zsh/site-functions" "$HOMEBREW_PREFIX/share/zsh-completions"; do
    if [[ -d "$DIR" ]] && [[ !${fpath[(Ie)$DIR]} ]]; then
      fpath+=($DIR)
    fi
  done

  # Hint: The (N,F) suffix is a ZSH glob qualifier
  #   N: Turns on NULL_GLOB to avoid erroring out if there are no matches
  #   F: Only matches "full" (non-empty) directories

  # Add Homebrew gnubin folders to path
  for DIR in "$HOMEBREW_PREFIX"/opt/*/libexec/gnubin(N,F); do
    if [[ !${path[(Ie)$DIR]} ]]; then
      path=("$DIR" $path)
    fi
  done
  # Add Homebrew gnuman folders to manpath
  for DIR in "$HOMEBREW_PREFIX"/opt/*/libexec/gnuman(N,F); do
    if [[ !${manpath[(Ie)$DIR]} ]]; then
      manpath=("$DIR" $manpath)
    fi
  done
  # Add Homebrew keg-only formulae to path (esp Ruby, as macOS Ruby is obsolete)
  for OPT in sqlite ruby; do
    DIR="$HOMEBREW_PREFIX/opt/$OPT/bin"
    if [[ -d "$DIR" ]] && [[ !${path[(Ie)$DIR]} ]]; then
      path=("$DIR" $path)
    fi
  done
fi

# Add local bin paths
for DIR in "$HOME/.cargo/bin" "$HOME/.local/bin" "$HOME/bin"; do
  if [ -d "$DIR" ] && [[ !${path[(Ie)$DIR]} ]]; then
    path=("$DIR" $path)
  fi
done

# Remove temp variables
unset BREW_LOCATION
unset OPT
unset DIR
