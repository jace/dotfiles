# Setup Homebrew and $PATH

if [[ "$OSTYPE" == *android* ]]; then
  # Do not probe root-level folders on Android.
  # Assume no Homebrew
  BREW_LOCATION=

# Disregard any `brew` already in the path and probe
# for specific folders. This avoids an edge case with
# macOS on Apple Silicon when Homebrew for Intel is
# also installed, for legacy formulae like python2.7.
# /usr/local/bin is in the default macOS $PATH and
# the legacy Homebrew installation will be there

elif [[ -x /opt/homebrew/bin/brew ]]; then
  # macOS on Apple Silicon
  BREW_LOCATION="/opt/homebrew/bin/brew"
elif [[ -x /usr/local/bin/brew ]]; then
  # Legacy macOS on Intel
  BREW_LOCATION="/usr/local/bin/brew"
elif [[ -x /home/linuxbrew/.linuxbrew/bin/brew ]]; then
  # Homebrew Linux global (formerly Linuxbrew)
  BREW_LOCATION="/home/linuxbrew/.linuxbrew/bin/brew"
elif [[ -x "$HOME/.linuxbrew/bin/brew" ]]; then
  # Homebrew Linux local
  BREW_LOCATION="$HOME/.linuxbrew/bin/brew"
else
  # No Homebrew
  BREW_LOCATION=
fi

# If we have Homebrew, setup its environment
if [ -n "$BREW_LOCATION" ]; then
  eval "$("$BREW_LOCATION" shellenv)"

  # Set HOMEBREW_PREFIX if necessary
  if [[ -z "$HOMEBREW_PREFIX" ]]; then
    export HOMEBREW_PREFIX="$(brew --prefix)"
  fi

  # Add ZSH completions to $fpath
  for DIR in "$HOMEBREW_PREFIX/share/zsh/site-functions" "$HOMEBREW_PREFIX/share/zsh-completions"; do
    if [[ -d "$DIR" ]] && [[ !${fpath[(Ie)$DIR]} ]]; then
      fpath+=($DIR)
    fi
  done

  # Hint: The (N,F) suffix is a ZSH glob qualifier
  #   N: Turns on NULL_GLOB to avoid erroring if there are no matches (skips the for loop instead)
  #   F: Only matches "full" (non-empty) directories

  # Add Homebrew gnubin folders to $path
  for DIR in "$HOMEBREW_PREFIX"/opt/*/libexec/gnubin(N,F); do
    if [[ !${path[(Ie)$DIR]} ]]; then
      path=("$DIR" $path)
    fi
  done
  # Add Homebrew gnuman folders to $manpath
  for DIR in "$HOMEBREW_PREFIX"/opt/*/libexec/gnuman(N,F); do
    if [[ !${manpath[(Ie)$DIR]} ]]; then
      manpath=("$DIR" $manpath)
    fi
  done

  # Add some Homebrew keg-only formulae to $path.
  # Homebrew's Ruby is keg-only for unclear reasons,
  # but various other tools expect a modern `ruby` in the path,
  # so we're going to put it there until we find a compelling reason not to.
  # Same for a few others

  for OPT in curl ruby; do
    DIR="$HOMEBREW_PREFIX/opt/$OPT/bin"
    if [[ -d "$DIR" ]] && [[ !${path[(Ie)$DIR]} ]]; then
      path=("$DIR" $path)
    fi
  done
fi

# Add local bin paths
for DIR in "$HOME/.cargo/bin" "$HOME/.local/bin" "$HOME/bin"; do
  if [ -d "$DIR" ] && [[ !${path[(Ie)$DIR]} ]]; then
    path=("$DIR" $path)
  fi
done

# Unset temp variables
unset BREW_LOCATION
unset OPT
unset DIR
